apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy.MM.dd", TimeZone.getTimeZone("Asia/Shanghai"))
}


android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "com.gochinatv.ad"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 15
        versionName "2.3.1"
        // dex突破65535的限制
        multiDexEnabled true
        manifestPlaceholders = [UMENG_APPKEY_VALUE: "572c1246e0f55aa6c5001533"]
    }
    // 禁用lint报错
    lintOptions {
        abortOnError false
        // 防止在发布的时候出现因MissingTranslation导致Build Failed!
        disable 'MissingTranslation'
    }
    buildTypes {
        debug {
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            // 是否使用混淆。
            minifyEnabled false
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为vego_v1.0_2015.01.15_wandoujia.apk
                        def fileName = "cr_v${defaultConfig.versionName}_${releaseTime()}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    // 友盟多渠道打包
    productFlavors {
        chinarestaurant {}
        ctTest {}
        RKM {}
        RKMtest {}
        zhaoge {}
        zhaogetest {}

    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile project(':dlutils')
    compile project(':db')
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile project(':statistics')
    compile 'com.jakewharton:butterknife:8.0.1'
    compile project(path: ':library')
    compile project(':jsBridge')
    compile 'com.google.firebase:firebase-messaging:9.2.1'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:support-v4:24.0.0'
    compile project(':retrofitUtils')
}

apply plugin: 'com.google.gms.google-services'
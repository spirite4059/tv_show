apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy.MM.dd", TimeZone.getTimeZone("Asia/Shanghai"))
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.gochinatv.ad"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 12
        versionName "1.1.9"
        // dex突破65535的限制
        multiDexEnabled true
        //    5697616167e58e3881002b8e
        manifestPlaceholders = [UMENG_APPKEY_VALUE: "56aad2f3e0f55a8655002111"]
    }
    // 禁用lint报错
    lintOptions {
        abortOnError false
        // 防止在发布的时候出现因MissingTranslation导致Build Failed!
        disable 'MissingTranslation'
    }
    buildTypes {
        debug {
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            // 是否使用混淆。
            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为vego_v1.0_2015.01.15_wandoujia.apk
                        def fileName = "cr_v${defaultConfig.versionName}_${releaseTime()}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    // 友盟多渠道打包
    productFlavors {
        chinarestaurant {}
        ctTest {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile project(':httpUtils')
    compile project(':dlutils')
    compile project(':library')
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.android.support:recyclerview-v7:23.0.1'
}
